apply plugin: 'com.android.library'
apply plugin: 'kotlin-android'
apply plugin: 'kotlin-android-extensions'

apply plugin: "de.mannodermaus.android-junit5"

android {
    compileSdkVersion 26
    buildToolsVersion '26.0.2'

    defaultConfig {
        minSdkVersion 19
        targetSdkVersion 26
        versionCode 1
        versionName "1.0"

        testInstrumentationRunner "android.support.test.runner.AndroidJUnitRunner"

    }
    buildTypes {
        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
        }
    }

    sourceSets {
        main.java.srcDirs += 'src/main/kotlin'
        test.java.srcDirs += 'src/test/kotlin'
    }
}

androidExtensions {
    // So we can use @Parcelize.  This does not expose risk to consumers; this is a build time-only
    // consideration.
    experimental = true
}

dependencies {
    compile fileTree(dir: 'libs', include: ['*.jar'])
    androidTestCompile('com.android.support.test.espresso:espresso-core:2.2.2', {
        exclude group: 'com.android.support', module: 'support-annotations'
    })
    compile 'com.android.support:appcompat-v7:26.1.0'
    compile 'com.android.support:transition:26.1.0'

    // used only by the UI concerns
    compile 'com.android.support:recyclerview-v7:26.1.0'
    compile 'com.android.support:design:26.1.0'

    testCompile 'junit:junit:4.12'
    testCompile 'org.jetbrains.spek:spek-api:1.1.5'
    testCompile 'org.jetbrains.spek:spek-junit-platform-engine:1.1.5'
    testCompile 'com.natpryce:hamkrest:1.4.2.0'
    testCompile 'org.amshove.kluent:kluent:1.30'
    testCompile 'org.amshove.kluent:kluent-android:1.30'
    testCompile 'org.skyscreamer:jsonassert:1.5.0'

    // JUnit 5 support.  Needed for Spek.
    testImplementation junit5()
    // (Optional) If you need "parameterized tests"
    testImplementation junit5Params()
    // (Optional) For running tests inside Android Studio 3.x (needed as of at least beta 7)
    testCompileOnly junit5EmbeddedRuntime()

    compile "org.jetbrains.kotlin:kotlin-stdlib-jre7:$kotlin_version"

    implementation 'com.facebook.stetho:stetho:1.5.0'
    // so we can inject Stetho HTTP monitoring into the Rover SDK
    implementation 'com.facebook.stetho:stetho-urlconnection:1.5.0'
    // so we can inject Stetho HTTP monitoring into the Retrofit requests made by the Experiences app.
    implementation 'com.facebook.stetho:stetho-okhttp3:1.5.0'
}

repositories {
    mavenCentral()
    jcenter()
}


// The following needed for ktlint:
configurations {
    ktlint
}
dependencies {
    ktlint "com.github.shyiko:ktlint:0.15.0"
    // additional 3rd party ruleset(s) can be specified here
    // just add them to the classpath (e.g. ktlint 'groupId:artifactId:version') and
    // ktlint will pick them up
}
task ktlint(type: JavaExec, group: "verification") {
    description = "Check Kotlin code style."
    classpath = configurations.ktlint
    main = "com.github.shyiko.ktlint.Main"
    args "src/**/*.kt"
    // to generate report in checkstyle format prepend following args:
    // "--reporter=plain", "--reporter=checkstyle,output=${buildDir}/ktlint.xml"
    // see https://github.com/shyiko/ktlint#usage for more
}
check.dependsOn ktlint
task ktlintFormat(type: JavaExec, group: "formatting") {
    description = "Fix Kotlin code style deviations."
    classpath = configurations.ktlint
    main = "com.github.shyiko.ktlint.Main"
    args "--android", "-F", "src/**/*.kt"
}
